title: Petition of the Workers
subtitle: Our Worker support base have pushed forward demands.
view-if: ((class1_set = 'workers' and class1_timer <= 0) or (class2_set = 'workers' and class2_timer <= 0)) and party_demands_timer <= 0 and (psi_majority or left_wing_coalition) and womens_suffrage
on-arrival: {! 

Q.party_demands_timer = 6;

if (Q.class1_set == "workers") {
    Q.class1_timer = 12;
}

if (Q.class2_set == "workers") {
    Q.class2_timer = 12;
}
!}
new-page: true
tags: event

= Petition of the Workers

The Workers, our traditional and key voting support bloc have pushed forward a series of demands we should do in order to keep their support.

We will have to choose one to complete, or we could refuse their demands entirely.

- @workers_demand_bordiga: Nationalise industries without compensation.
- @workers_demand_serrati: Expand the scope of the Unemployment Insurance Program.
- @workers_demand_turati: Cut taxes for the lower class.
- @workers_demand_wages: Develop and enforce a minimum wage.
- @workers_demand_safety: Develop and enforce safety regulations.
- @workers_demand_refuse: Refuse the demands.

@workers_demand_bordiga
view-if: max_seize < 3 and prime_minister = "Bordiga"
on-arrival: max_seize += 1; unemployment -= 0.4 if unemployment >= 8; workers_psi += 6*(1-dissent); veterans_psi += 4*(1-dissent); middle_class_pnf += 3; radicalisation += 0.3; unemployment -= 0.7; economic_growth -= 0.4; industrialists_pnf += 8; capital_strike_progress += 3; fasci_coup_progress += 2; ldr_relation -= 10; ul_relation -= 10; ppi_relation -= 4; ds_right += 1; ldr_right += 1; ppi_right += 0.5; socialist_sentiment += 3;

We begin forcefully seizing idle industries, nationalising vast sums of the economy without compensating the greedy industrialists a single cent. They are, of course, outraged, and utilise their backings and support from the liberal parties. The [+ pnf_name +] use this as propaganda, fearmongering to the middle class and other industrialists who are swayed to support them.

@workers_demand_serrati
subtitle: -2 Budget
view-if: unemployment_insurance_founded and prime_minister = "Serrati"
choose-if: not national_economic_recovery or budget > 1
unavailable-subtitle: Not enough budget. (Need 2)
on-arrival: {!
Q.unemployment_insurance_expanded = 1; 
Q.insurance_payments_veterans_timer = 24; 
Q.budget -= 2; 


Q.workers_psi += 4*(1-Q.dissent);
Q.workers_pnf -= 4*(1-Q.dissent); 

Q.economic_growth += 0.4; 
Q.inflation += 0.8;
Q.psri_relation += 7*(1-Q.dissent);
Q.nationalist_sentiment -= 3;
!}

Now that our financial situation is better, we can expand the scope of the program further, at least temporarily.

@workers_demand_turati
view-if: lower_tax_rates > -5 and prime_minister = "Turati"
on-arrival: {!

Q.workers_psi += 4*(1-Q.dissent); 
Q.middle_class_psi -= 3*(1-Q.dissent); 
Q.industrialists_psi -= 2*(1-Q.dissent);
Q.veterans_psi += 2*(1-Q.dissent);

Q.lower_tax_rates -= 1; 

Q.psri_relation += 3*(1-Q.dissent);
Q.ppi_relation += 2*(1-Q.dissent);
Q.ldr_relation -= 3*(1-Q.dissent); 
Q.ul_relation -= 2*(1-Q.dissent); 

Q.inflation += 0.1; 
Q.economic_growth += 0.1;
Q.budget -= 1;
!}

The liberal parties berate our actions, criticising our lack of care for the economy and the current economic situation. They use this as a recruiting tool amongst the industrialists and middle class, while we see an increase in support from our workers.

@workers_demand_wages
on-arrival: wage_increase += 1; labour_dissent -= 6; labour_strength += 3; workers_psi += 5*(1-dissent); capital_strike_progress += 1 if wage_increase >= 3; socialist_sentiment += 1 if wage_increase >= 3; economic_growth -= 0.3; unemployment += 0.2; inflation -= 0.2; economic_growth -= 0.2 if wage_increase >= 3; unemployment += 0.1 if wage_increase >= 3; inflation -= 0.2 if wage_increase >= 3; 

The Labour Ministry works on developing and enforcing a standard national minimum wage, a new concept developed recently and popularised in the United Kingdom. [? if wage_increase >= 3 : Businesses have begun to argue that the minimum wage is too high, interfering with the economy. ?]

@workers_demand_safety
on-arrival: workers_safety += 1; labour_dissent -= 5; syndicalist_dissent -= 3 if syndicalist_active; labour_strength += 2; workers_psi += 3*(1-dissent); capital_strike_progress += 1 if workers_safety >= 3; socialist_sentiment += 1 if workers_safety >= 3;

The Labour Ministry works on developing and enforcing safety regulations, in order to ensure healthy working conditions for the proletariat. [? if workers_safety >= 3 : Businesses have begun to argue that our focus on safety is overbearing and unnecessary, interfering with the economy. ?]

@workers_demand_refuse
on-arrival: {!
Q.workers_demand_failed += 1;
Q.workers_psi -= 4*Q.workers_demand_failed;

if (Q.left_wing_coalition) {
    Q.coalition_dissent += 1;
}

Q.labour_dissent += 5;
!}

The workers are, unsurprisingly, outraged at our refusal to heed their demands, as is the Labour faction.