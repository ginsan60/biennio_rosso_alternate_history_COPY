title: Judiciary Reform
new-page: true
is-card: true
view-if: psi_in_government = 1 and justice_minister_party = psi_name and judiciary_reform_timer <= 0
on-arrival: week_actions += 1; judiciary_reform_timer = 3;
card-image: img/portraits/ClaudioTreves.jpg
tags: govt_affairs

= Judiciary Reform

The Judiciary is one of the most reactionary components of the government, along with the military. Many jurists have held office since the days of the Risorgimento. They give violent fascists on the right laughably light sentences, while ruthlessly persecuting leftists and pacifists for lesser crimes.

[? if judicial_reform <= 0 : There is great demand in our membership for reforming the judiciary, although this may bring us into a collision course with our coalition allies. ?]
[? if judicial_reform == 1 : We have started on the path to judicial reform. While some of the most antediluvian and biased justices have been replaced, many still remain. More should be done. ?]
[? if judicial_reform >= 2 : We have successfully reformed the judiciary, to some extent. At the very least, the courts will not actively obstruct our attempts to prosecute crimes committed by the far-right. ?]

- @reform: Carry out substantial judicial reforms,
- @minor_reforms: Carry out minor judicial reforms.
- @investigate_mussolini: Investigate Mussolini's crimes.
- @as_is: Leave the judiciary as it is.
- @easy_discard
- @cancel_advisor_action

@reform
subtitle: -1 Budget [? if liberal_budget_accepted and budget <= 0 : This will put us into a deficit! ?]
choose-if: not national_economic_recovery or budget > 0
unavailable-subtitle: Not enough budget. (Need 1)
on-arrival: budget -= 1; democratisation += 2; coalition_dissent += 1 if centre_left_coalition; workers_psi += 5*(1-dissent) if democratisation >= 3 and not in_unstable_coalition; workers_clc += 5*(1-dissent) if democratisation >= 3 and centre_left_coalition; workers_scc += 5*(1-dissent) if democratisation >= 3 and socialist_catholic_coalition; judicial_reform += 2; republican_sentiment += 5*(1-dissent); fasci_coup_progress += 1; ldr_promise_failed = 1 if (budget < 0 and liberal_budget_accepted); socialist_sentiment += 2
go-to: ldr_promise_vonc if ldr_promise_failed = 1

We investigate jurists who have shown themselves to have biased or corrupt behavior, and try to remove them, replacing them with more democratically-oriented jurists. This obviously raises quite a great deal of ire in conservative circles, including within our coalition. 

@minor_reforms
on-arrival: democratisation += 1; workers_psi += 3*(1-dissent) if democratisation >= 3; judicial_reform += 1; republican_sentiment += 3*(1-dissent); nationalist_sentiment -= 1*(1-dissent);

We try to remove the most obviously biased and corrupt jurists, while leaving most of the system intact. It is slow work reforming the judiciary.

@investigate_mussolini
view-if: mussolini_jailed = 1 and mussolini_convicted = 0
on-arrival: evidence += 1; 

We are able to gain warrants, raiding hideouts and investigating Mussolini's closest confidants and interrogating them along the way.

@as_is
go-to: root