title: Campaigning
new-page: true
on-arrival: week_actions += 1
view-if: not_participating = 0
is-card: true
card-image: img/Campaigning.jpg
frequency: 100
tags: party_affairs

= Campaigning

Whether or not we are in an electoral campaign, the party is always trying to broaden its voter base. Who should we focus our campaign efforts on?

- @workers: The urban working class, our traditional support base.
- @middle_class: The middle class of small independent businessmen and artisans.
- @industrialists: The industrialists and landowners who traditionally oppose us.
- @rural: Rural workers and small farmers.
- @veterans: Newly unemployed veterans discharged from the army.
- @catholics: Catholics, who typically support the People's Party.
- @women: Women, who typically support the People's Party.
- @root: We either don't want to or cannot campaign right now.
- @easy_discard

@workers
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources. (Need 1)
on-arrival: workers_psi += 6*(1-dissent); workers_psi -= 2 if radicalisation > 8; workers_psi -= 2 if unemployment >= 15 and psi_in_government; workers_psi -= 2 if unemployment >= 25 and psi_in_government; resources -= 1; workers_psi += 3*(1-dissent) if nationalisation_progress >= 1;

We have increased our campaigning among the workers. [? if unemployment >= 15: The high levels of unemployment make our campaign more difficult. ?]

@middle_class
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources. (Need 1)
on-arrival: middle_class_psi += 6*(1-dissent); middle_class_psi -= 3 if anti_monarchist_sentiment <= 49; resources -= 1; middle_class_psi -= 2*(1-dissent) if nationalisation_adopted

We have increased our campaigning among the middle class. [? if socialist_sentiment <= 49: Our campaign is worsened by their wariness of our ideals. ?][? if nationalisation_adopted : Our campaign is worsened by our radical political & economic plan. ?]

@industrialists
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources. (Need 1)
on-arrival: industrialists_psi += 5*(1-dissent); resources -= 1; industrialists_psi -= 1 if nationalist_sentiment >= 51; industrialists_psi -= 3 if nationalisation_adopted; industrialists_psi += 2 if welfare < 0

We have increased our campaigning among the industrialists. Our ideals are diametrically opposed to them, making campaigning harder. [? if nationalisation_adopted : Our campaign is worsened by our radical political & economic plan. ?] [? if welfare < 0 : Our campaign is made easier by the gutting of the welfare system. ?]

@rural
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources. (Need 1)
on-arrival: rural_psi += 6*(1-dissent); rural_psi -= 3*(1-dissent) if socialist_sentiment > 49; resources -= 1; rural_psi += 3*(1-dissent) if agricultural_policy >= 2

We have increased our campaigning among farmers. [? if agricultural_policy >= 2 : Our rural policies have made it easier for us to build support among farmers. ?]

@veterans
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources. (Need 1)
on-arrival: veterans_psi += 6*(1-dissent); resources -= 1; veterans_psi -= 3 if welfare < 0; veterans_psi += 3*(1-dissent) if welfare >= 2;

We have increased our campaigning among the unemployed. [? if welfare < 0: The welfare cuts and resultant poverty make our campaign more difficult, since we are associated with the government that enacted these cuts, and our campaign may even backfire. ?]

@catholics
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources. (Need 1)
on-arrival: catholic_psi += 6*(1-dissent); resources -= 1; catholic_psi -= 5*(1-dissent) if secularised_education; catholic_psi += 2*(1-dissent) if roman_question_answered; catholic_psi += 1 if pope = "Gasppari" and roman_question_answered; catholic_psi -= 1 if pope = "Del Val" and roman_question_answered

We have increased our campaigning among Catholics. [? if secularised_education : Our campaign is made harder due to our decision to secularise education. ?] [? if roman_question_answered : The signing of the Lateran Treaty under our Foreign Minister makes building support easier. ?]

[? if pope = "Del Val" and roman_question_answered: A Pope antagonistic to our cause has made campaigning slightly harder. ?] [? if pope = "Gasppari" and roman_question_answered: A Pope sympathetic to our cause has made campaigning easier. ?]

@women
subtitle: -1 resources
view-if: womens_suffrage
choose-if: resources >= 1
unavailable-subtitle: Not enough resources. (Need 1)
on-arrival: women_psi += 6*(1-dissent); resources -= 1; women_psi += 2*(1-dissent) if abortion_rights; women_psi += 2*(1-dissent) if womens_rights > 2; 

We have increased our campaigning among Women. [? if womens_rights > 2 : Our reforms to women's rights have made it easier for us to build support. ?]