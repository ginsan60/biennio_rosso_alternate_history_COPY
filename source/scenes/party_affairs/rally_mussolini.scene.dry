title: Rallying against Mussolini
new-page: true
view-if: rally_mussolini_timer = 0 and pnf_in_government and pnf_leader = "Mussolini"
on-arrival: rally_mussolini_timer = 5; week_actions += 1; rg_militarisation_cost = 2 if rg_militancy > 0.5 else rg_militarisation_cost = 1; rg_militarisation_cost = 3 if rg_militancy > 0.75
frequency: 150
is-card: true
card-image: img/portraits/BenitoMussolini.jpg
tags: party_affairs

= Rallying against Mussolini

Our worst fears have come true. A fascist is in control of Italy, assuming the office of Prime Minister. Benito Mussolini. Former member of the [+ psi_name +] and dangerous â€“ politically ambiguous and apt, he will seemingly do everything in his power to maintain power.

What should we do?

- @mussolini_rally: Rally against Mussolini's authoritarianism and violence.
- @mussolini_relation: Strengthen ties with the other parties.
- @mussolini_fund: Continue funding the Red Guards to oppose the Squadrismo.
- @mussolini_dissent: Unify the party and reduce dissent.
- @root: Do nothing for now.
- @easy_discard

@mussolini_rally
subtitle: Emphasising Mussolini's desperation to cling to power might sway some supporters away from him.
on-arrival: workers_pnf -= 2; rural_pnf -= 2; workers_psi += 2; 

We rally workers and farmhands against Mussolini, weakening his support base.

@mussolini_relation
subtitle: Mussolini's radicalism mean we might be able to sway the other parties against supporting him.
on-arrival: ds_relation += 5; psri_relation += 5; ldr_relation += 5; ppi_relation += 5;

We reaffirm our ties with our fellow left-wing parties, as well as cultivate ties with the liberals and Catholics to aid us against Mussolini should the time come.

@mussolini_fund
subtitle: -1 Paramilitary Resources 
choose-if: para_resources >= rg_militarisation_cost
unavailable-subtitle: Not enough paramilitary resources. (Need [+ rg_militarisation_cost +])
on-arrival: para_resources -= rg_militarisation_cost; rg_investment += 1; rg_strength += 15*revolutionary_exhaustion; rg_militancy += 0.04*revolutionary_exhaustion; communist_strength += 2; communist_dissent -= 2; reformist_dissent += 5; radicalisation += 0.1; workers_psi += 2*(1-dissent); veterans_psi += 2*(1-dissent); middle_class_psi -= 5*(1-dissent); rg_militancy += 0.02 if veterans_psi_normalised >= 40; rg_strength += 5 if veterans_psi_normalised >= 40

We continue funding the Red Guards, who begin recruitment of more members and are able to acquire more effective weaponry.

@mussolini_dissent
subtitle: The party has to be united in this time of crisis.
on-arrival: communist_dissent -= 5; maximalist_dissent -= 5; labour_dissent -= 5; syndicalist_dissent -= 5 if syndicalist_active; reformist_dissent -= 5;

Though the party is typically a squabbling mess, we are able to quell dissent in order to face fascism as a united front.